import org.gradle.internal.os.OperatingSystem

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

version = mdcLibraryVersion

dependencies {
  api compatibility("annotation")
  api compatibility("appcompat")
  api compatibility("cardview")
  api compatibility("coordinatorlayout")
  api compatibility("constraintlayout")
  api compatibility("core")
  api compatibility("drawerlayout")
  api compatibility("dynamicanimation")
  api compatibility("experimental")
  api compatibility("fragment")
  api compatibility("lifecycleRuntime")
  api compatibility("recyclerview")
  api compatibility("transition")
  api compatibility("vectordrawable")
  api compatibility("viewpager2")

  testImplementation "androidx.test:core:${testRunnerVersion}"
  testImplementation "androidx.test:runner:${testRunnerVersion}"
  testImplementation "junit:junit:4.13.2"
  testImplementation "com.google.truth:truth:${truthVersion}"
  testImplementation "org.mockito:mockito-core:${mockitoCoreVersion}"
  testImplementation "org.robolectric:robolectric:${robolectricVersion}"

  testAnnotationProcessor "com.google.auto.service:auto-service:1.0-rc4"
}

def srcDirs = [
  'com/bottombar/navigation/material/animation',
  'com/bottombar/navigation/material/appbar',
  'com/bottombar/navigation/material/badge',
  'com/bottombar/navigation/material/behavior',
  'com/bottombar/navigation/material/bottomappbar',
  'com/bottombar/navigation/material/bottomnavigation',
  'com/bottombar/navigation/material/bottomsheet',
  'com/bottombar/navigation/material/button',
  'com/bottombar/navigation/material/canvas',
  'com/bottombar/navigation/material/card',
  'com/bottombar/navigation/material/checkbox',
  'com/bottombar/navigation/material/chip',
  'com/bottombar/navigation/material/circularreveal',
  'com/bottombar/navigation/material/circularreveal/cardview',
  'com/bottombar/navigation/material/circularreveal/coordinatorlayout',
  'com/bottombar/navigation/material/color',
  'com/bottombar/navigation/material/datepicker',
  'com/bottombar/navigation/material/dialog',
  'com/bottombar/navigation/material/divider',
  'com/bottombar/navigation/material/drawable',
  'com/bottombar/navigation/material/elevation',
  'com/bottombar/navigation/material/expandable',
  'com/bottombar/navigation/material/floatingactionbutton',
  'com/bottombar/navigation/material/imageview',
  'com/bottombar/navigation/material/internal',
  'com/bottombar/navigation/material/materialswitch',
  'com/bottombar/navigation/material/math',
  'com/bottombar/navigation/material/menu',
  'com/bottombar/navigation/material/motion',
  'com/bottombar/navigation/material/navigation',
  'com/bottombar/navigation/material/navigationrail',
  'com/bottombar/navigation/material/progressindicator',
  'com/bottombar/navigation/material/radiobutton',
  'com/bottombar/navigation/material/resources',
  'com/bottombar/navigation/material/ripple',
  'com/bottombar/navigation/material/shape',
  'com/bottombar/navigation/material/shadow',
  'com/bottombar/navigation/material/slider',
  'com/bottombar/navigation/material/snackbar',
  'com/bottombar/navigation/material/stateful',
  'com/bottombar/navigation/material/switchmaterial',
  'com/bottombar/navigation/material/tabs',
  'com/bottombar/navigation/material/textfield',
  'com/bottombar/navigation/material/textview',
  'com/bottombar/navigation/material/theme',
  'com/bottombar/navigation/material/theme/overlay',
  'com/bottombar/navigation/material/timepicker',
  'com/bottombar/navigation/material/tooltip',
  'com/bottombar/navigation/material/transition',
  'com/bottombar/navigation/material/transformation',
  'com/bottombar/navigation/material/typography',
]

android {
  sourceSets {
    main.manifest.srcFile 'java/com/bottombar/navigation/material/AndroidManifest.xml'
    main.java.srcDir 'java'
    main.java.includes = srcDirs.collect { it + '/**/*.java' }
    main.java.excludes = [
      '**/build/**',
    ]
    srcDirs.forEach {
      main.res.srcDirs += 'java/' + it + '/res'
      main.res.srcDirs += 'java/' + it + '/res-public'
      test.res.srcDirs += 'javatests/' + it + '/res'
    }

    test.java.srcDir 'javatests'
  }

  testOptions.unitTests.includeAndroidResources = true

  buildTypes.all {
    consumerProguardFiles 'proguard-behaviors.pro', 'proguard-inflater.pro'
  }

  defaultConfig {
    minSdkVersion 14
  }
}

task generateJavadocs(type: Javadoc) {
  if (project.hasProperty("online")) {
    options.addStringOption("toroot", "/")
    options.addStringOption("hdf", "android.whichdoc online")
    options.addStringOption("hdf", "dac")
    options.addBooleanOption("devsite", true)
    options.addBooleanOption("yamlV2", true)
    options.addStringOption("dac_libraryroot", "com/bottombar/navigation/material")
    options.addStringOption("dac_dataname", "MATERIAL_DATA")
  }

  if (project.hasProperty("docletPathRoot")) {
    def docletPathRoot = project.property("docletPathRoot")
    def outputPath = project.hasProperty("outputPath") ? project.property("outputPath") : "doclava-out"

    source = android.sourceSets.main.java.source
    source = source.findAll { it.name.endsWith(".java") }

    title = null
    destinationDir = new File(outputPath)
    classpath = files("${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar")
    options.addStringOption("federate Android", "https://developer.android.com")
    options.encoding = "UTF-8"
    options.doclet = "com.google.doclava.Doclava"
    options.docletpath = [
      file(docletPathRoot + "/doclava/current/doclava.jar"),
      file(docletPathRoot + "/jsilver/v1_0_0/jsilver.jar")
    ]
  }
}

task getVersion {
  doLast {
    println version
  }
}

task generateApiXml(type: Javadoc) {
  if (project.hasProperty("apiName")) {
    def jdiff = project.property("jdiffJar")
    def apiName = project.property("apiName")
    source = android.sourceSets.main.java.source
    source = source.findAll { it.name.endsWith(".java") }
    classpath = files("${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar")
    options.doclet = "jdiff.JDiff"
    options.addStringOption("subpackages", ".")
    options.addStringOption("apiname", apiName)
    options.docletpath = [
      file(jdiff),
    ]

    // Doclava does not understand -notimestamp option that is default since Gradle 6.0
    options.setNoTimestamp(false)
  }

  doLast {
    // Escape incorrect ampersands in API XML file
    if (OperatingSystem.current().isLinux()) {
      ["sed", "-i", "s/ & / \\&amp; /g", "lib/${apiName}.xml"].execute()
    } else {
      ["sed", "-i", "''", "s/ & / \\&amp; /g", "lib/${apiName}.xml"].execute()
    }
  }
}

task generateJdiffReport(type: Javadoc) {
  if (project.hasProperty('oldApi')) {
    def outputPath = project.hasProperty('outputPath') ? project.property('outputPath') : 'diffs-out'
    def jdiff = project.property('jdiffjar')
    def xerces = project.property('xercesjar')
    def oldApi = project.property('oldApi')
    def newApi = project.property('newApi')
    def newApiDir = project.property('newApiDir')
    def oldApiDir = project.property('oldApiDir')
    destinationDir = new File(outputPath)
    source = android.sourceSets.main.java.source
    source = source.findAll { it.name.endsWith('.java') }
    classpath = files('${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar')
    options.doclet = 'jdiff.JDiff'
    options.addStringOption('subpackages', '.')
    options.addStringOption('newapidir', newApiDir)
    options.addStringOption('oldapidir', oldApiDir)
    options.addStringOption('oldapi', oldApi)
    options.addBooleanOption('verbose', true)
    options.addStringOption('newapi', newApi)
    // Doclava does not understand -notimestamp option that is default since Gradle 6.0
    options.setNoTimestamp(false)
    options.docletpath = [
      file(jdiff),
      file(xerces),
    ]
  }
}

def R_CLASS_PATH = "build/generated/not_namespaced_r_class_sources/releaseUnitTest/processReleaseUnitTestResources/r/com/bottombar/navigation/material/R.java"
Attribute<String> ARTIFACT_TYPE = Attribute.of("artifactType", String.class)
afterEvaluate {
  [generateJavadocs, generateApiXml].forEach { task ->
    task.dependsOn(':lib:processReleaseUnitTestResources')
    task.source += R_CLASS_PATH

    def releaseVariant = android.libraryVariants.find { it.name == 'release' }
    if (releaseVariant == null) {
      return
    }

    // Add transitive runtime dependencies to classpath
    task.classpath += releaseVariant.runtimeConfiguration.incoming.artifactView { aView ->
      aView.attributes.attribute(ARTIFACT_TYPE, "android-classes")
    }.files

    // Add project and compile dependencies to classpath
    releaseVariant.javaCompileProvider.configure { javaCompileProvider ->
      task.classpath += releaseVariant.getCompileClasspath(null)
      task.classpath += task.project.files(javaCompileProvider.destinationDir)
      task.source += javaCompileProvider.source
    }

    // Doclava does not understand -notimestamp option that is default since Gradle 6.0
    task.options.setNoTimestamp(false)
  }
}

task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from(android.sourceSets.main.java.srcDirs) {
    // Needed because we have Java sources and resources in same directory
    include '**/*.java'
    includeEmptyDirs = false
  }
}

afterEvaluate {
  publishing {
    repositories {
      maven {
        url = "$mavenRepoUrl"
      }
    }

    publications {
      release(MavenPublication) {
        from components.release

        artifact androidSourcesJar

        groupId = 'com.bottombar.navigation.material'
        artifactId = 'material'
        version project.version
        pom {
          name = 'Material Components for Android'
          description = 'Material Components for Android is a static library ' +
            'that you can add to your Android application in order to use ' +
            'APIs that provide implementations of the Material Design specification. ' +
            'Compatible on devices running API 14 or later.'
          url = 'https://github.com/material-components/material-components-android'
          inceptionYear = '2015'
          licenses {
            license {
              name = 'The Apache Software License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution = 'repo'
            }
          }
          developers {
            developer {
              name = 'The Android Open Source Project'
            }
          }
          scm {
            connection = 'scm:git:https://github.com/material-components/material-components-android.git'
            url = 'https://github.com/material-components/material-components-android'
          }
        }
      }
    }
  }
}
